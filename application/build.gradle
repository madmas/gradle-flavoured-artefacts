import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
plugins {
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'com.karakun.sample.Application'

ext {
    // These URLs are used when starting the tests via Gradle on the CLI
    variants = [
            [name: "customerA"],
            [name: "customerB"],
            [name: "customerC"]
    ]
}

configurations {
    customerAcompile
    customerBcompile
    customerCcompile

    customerAcompile.extendsFrom(compile)
    customerBcompile.extendsFrom(compile)
    customerCcompile.extendsFrom(compile)
}

dependencies {
    implementation project(':lib-implA')
    customerAcompile project(':lib-implA')
    customerBcompile project(':lib-implB')
    customerCcompile project(':lib-implC')
}

variants.each { variant ->
    task "${variant.name}ShadowJar"(type: ShadowJar) {
        classifier = "${variant.name}"
        from sourceSets.main.output
        configurations = [project.configurations."${variant.name}compile"]
    }
    shadowJar.dependsOn("${variant.name}ShadowJar")
}